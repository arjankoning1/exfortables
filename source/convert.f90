subroutine convert(i)
!
!-----------------------------------------------------------------------------------------------------------------------------------
! Purpose: Convert input line from upper case to lowercase
!
! Revision    Date      Author      Quality  Description
! ======================================================
!    1     05-12-2019   A.J. Koning    A     Original code
!-----------------------------------------------------------------------------------------------------------------------------------
!
! *** Use data from other modules
  use A0_exfortables_mod
!
! use A1_exfortables_mod, only: & ! Variables for reading input lines
!              inline           ! input line
!
! *** Declaration of local data
!
  implicit none
  integer, parameter :: numkey=3        ! number of keywords
  character(len=132) :: keyword(numkey) ! keyword
  character(len=132) :: str             ! input line
  integer            :: i               ! counter
  integer            :: k               ! counter
  integer            :: lkey            ! length of keyword
  integer            :: m               ! counter
!
! ************** Convert uppercase to lowercase characters *************
!
! For easy handling of all the input parameters, the whole input, both keywords and values, is converted to lowercase characters,
! with the exception of filenames or other character strings.
!
  data (keyword(m), m = 1, numkey) / 'filespath', 'libspath', 'talyspath'/
  str = inline(i)
  do k = 1, 132
    if (inline(i)(k:k) >= 'A' .and. inline(i)(k:k) <= 'Z') inline(i)(k:k) = achar(iachar(inline(i)(k:k)) + 32)
  enddo
  do k = 0, 110
    do m = 1, numkey
      lkey = len_trim(keyword(m))
      if (inline(i)(k+1:k+lkey) == trim(keyword(m))) then
        inline(i)(k + lkey + 1:132) = str(k + lkey + 1:132)
        return
      endif
    enddo
  enddo
end subroutine convert
! Copyright A.J. Koning 2019
